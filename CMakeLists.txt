cmake_minimum_required(VERSION 3.9)
  
project(rt-engine)

set(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)

include(setup)

find_package(XRT REQUIRED HINTS /opt/xilinx/xrt)
find_package(XRM)
if (${XRM_FOUND})
  message(STATUS "XRM was found at ${XRM_CMAKE_DIR}")
  add_definitions(-DXRM_FOUND)
endif()
if(CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)
  find_package(unilog REQUIRED)
  find_package(target-factory REQUIRED)
  find_package(xir REQUIRED)
  find_package(vart COMPONENTS runner trace REQUIRED)
endif()
find_package(glog REQUIRED)
get_target_property(GLOG_INCLUDE_DIRS glog::glog INTERFACE_INCLUDE_DIRECTORIES)
find_package(Protobuf REQUIRED) # rt-engine does not need
find_path(JSONC_INCLUDE_DIRS NAMES json-c)
find_library(JSONC_LIBRARIES NAMES json-c REQUIRED)
find_package(Threads REQUIRED)

add_subdirectory(engine)
add_subdirectory(runner)
add_subdirectory(controller)
add_subdirectory(device)

include_directories(
  ${XRT_INCLUDE_DIRS}
  ${XRM_INCLUDE_DIRS}
  ${target-factory_INCLUDE_DIRS}
  ${xir_INCLUDE_DIRS}
  ${vart_INCLUDE_DIRS}
  ${vart_INCLUDE_DIRS}/vart/experimental
  ${vart_INCLUDE_DIRS}/vart/trace
  ${ENGINE_HDRS}
  ${RUNNER_HDRS}
  ${CONTROLLER_HDRS}
  ${DEVICE_HDRS}
  ${GLOG_INCLUDE_DIRS}
  ${JSONC_INCLUDE_DIRS}
  )

add_library(
  ${PROJECT_NAME}
  SHARED 
  ${ENGINE_SRCS}
  ${RUNNER_SRCS}
  ${DEVICE_SRCS}
  ${CONTROLLER_SRCS}
  )
target_link_libraries(
  ${PROJECT_NAME}
  PRIVATE
  XRT::xrt_core
  XRT::xilinxopencl
  ${XRM_LIBRARIES}
  target-factory::target-factory 
  vart::runner
  vart::trace
  glog::glog
  ${JSONC_LIBRARIES}
  Threads::Threads
  stdc++fs
  )
set_target_properties(
  ${PROJECT_NAME}
  PROPERTIES
  INSTALL_RPATH ${CMAKE_INSTALL_PREFIX}/lib
  INSTALL_RPATH_USE_LINK_PATH TRUE
  )
install(
  TARGETS
  ${PROJECT_NAME}
  EXPORT ${PROJECT_NAME}-targets
  RUNTIME DESTINATION bin
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib)

if(BUILD_TESTS)
  enable_testing()
  add_subdirectory(tests)
endif()

if(UNIX)
  include(cpackLinux)
endif()

include(helper)
